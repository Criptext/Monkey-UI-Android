apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.criptext.circularaudioview'
version = '1.0.0'


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

task copyLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

bintray {
    user = 'criptext'
    key = '093138d874366bedf1953c28e317439845586a08'
    pkg {
        repo = 'maven'
        name = 'com.criptext.circularaudioview'

        version {
            name = '1.0.0'
            desc = 'CircularAudioView'
            released = new Date()
            vcsTag = '1.0.1'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Criptext/MonkeyUIAndroid'
        websiteUrl = 'http://criptext.com'
    }
    configurations = ['archives']
}